#include "8155.inc"

PUBLIC	PIO8155CTRLRD,	PIO8155CTRLWR,	PIO8155DATARD,	PIO8155DATAWR,	PIO8155TMRRD,	PIO8155TMRWR

DEVICE_8155		SEGMENT		CODE
RSEG DEVICE_8155

;READ FROM 8155 CONTROL PORT
;ON ENTRY:
; DPTR  = 8155 BASE ADDRESS
;ON RETURN:
; DPTR  = VALUE ON ENTRY
; C     = 0 IF SUCCESS
;   A = DATA
; C     = 1 IF FAIL
;   A = ERROR CODE
PIO8155CTRLRD	PROC
  ;SAVE REGISTERS
  PUSH  DPL
  PUSH  DPH
  ;CALCULATE ADDRESS OF PORT'S REGISTER
  XCH   A, DPL
  ADD   A, #PIO_8155_REGISTER_CONTROL
  XCH   A, DPL
  XCH   A, DPH
  ADDC  A, #0x00
  XCH   A, DPH
  ;READ FROM PORT
  MOVX  A, @DPTR
  ;RESTORE REGISTERS & RETURN
  POP   DPH
  POP   DPL
  CLR   C
  RET
ENDP


;WRITE TO 8155 CONTROL PORT
;ON ENTRY:
; A     =	DATA
; DPTR  = 8155 BASE ADDRESS
;ON RETURN:
; DPTR  = VLAUE ON ENTRY
; C     = 0 IF SUCCESS
;   A = 0x00
; C     = 1 IF FAIL
;   A = ERROR CODE
PIO8155CTRLWR	PROC
  ;SAVE REGISTERS
  PUSH  DPL
  PUSH  DPH
  ;CALCULATE ADDRESS OF PORT'S REGISTER
  XCH   A, DPL
  ADD   A, #PIO_8155_REGISTER_CONTROL
  XCH   A, DPL
  XCH   A, DPH
  ADDC  A, #0x00
  XCH   A, DPH
  ;WRITE TO PORT
  MOVX  @DPTR, A
  ;RESTORE REGISTERS & RETURN
  POP   DPH
  POP   DPL
  CLR   C
  RET
ENDP


;READ FROM 8155 DATA PORT
;ON ENTRY:
; R0    =	PORT NUMBER
; DPTR  = 8155 BASE ADDRESS
;ON RETURN:
; R0    = VALUE ON ENTRY
; DPTR  = VLAUE ON ENTRY
; C     = 0 IF SUCCESS
;   A = 0x00
; C     = 1 IF FAIL
;   A = ERROR CODE
PIO8155DATARD	PROC
MOV   A, #0x02
CLR   C
SUBB  A, R0
JNC   PIO8155DATARDA
;INVALID PORT NUMBER
MOV   A, #8155_ERR_INV_PORT
RET
PIO8155DATARDA:
;CALCULATE ADDRESS OF PORT'S REGISTER
PUSH  DPL
PUSH  DPH
XCH   A, DPL
ADD   A, R0
XCH   A, DPL
XCH   A, DPH
ADDC  A, #0x00
XCH   A, DPH
;READ FROM PORT
MOVX  A, @DPTR
;RESTORE REGISTERS & RETURN
POP   DPH
POP   DPL
CLR   C
RET
ENDP


;WRITE TO 8155 DATA PORT
;ON ENTRY:
; A     = DATA
; R0    = PORT NUMBER
; DPTR  = 8155 BASE ADDRESS
;ON RETURN:
; R0    = VALUE ON ENTRY
; DPTR  = VLAUE ON ENTRY
; C     = 0 IF SUCCESS
;   A = 0x00
; C     = 1 IF FAIL
;   A = ERROR CODE
PIO8155DATAWR	PROC
    PUSH  ACC
    MOV   A, #0x02
    CLR   C
    SUBB  A, R0
    POP   ACC
    JNC   PIO8155DATAWRA
    ;INVALID PORT NUMBER
    MOV   A, #8155_ERR_INV_PORT
    RET
  PIO8155DATAWRA:
    ;CALCULATE ADDRESS OF PORT'S REGISTER
    PUSH  DPL
    PUSH  DPH
    XCH   A, DPL
    ADD   A, R0
    XCH   A, DPL
    XCH   A, DPH
    ADDC  A, #0x00
    XCH   A, DPH
    ;WRITE TO PORT
    MOVX  @DPTR, A
    ;RESTORE REGISTERS & RETURN
    POP   DPH
    POP   DPL
    CLR   C
    RET
ENDP


;READ FROM 8155 TIMER
;ON ENTRY:
; DPTR	= 8155 BASE ADDRESS
;ON RETURN:
; DPTR  = VLAUE ON ENTRY
; C     = 0 IF SUCCESS
;   A   = 0x00
;   R0  = TIMER LSB
;   R1  = TIMER MSB
; C     = 1 IF FAIL
;   A   = ERROR CODE
;   R0  = 0x00
;   R1  = 0x00
PIO8155TMRRD	PROC
  ;SAVE REGISTERS
  PUSH  DPL
  PUSH  DPH
  ;CALCULATE ADDRESS OF PORT'S REGISTER
  MOV   A, DPL
  ADD   A, #PIO_8155_REGISTER_TMR_LO
  MOV   DPL, A
  MOV   A, DPH
  ADDC  A, #0x00
  MOV   DPH, A
  ;READ FROM PORT
  MOVX  A, @DPTR
  MOV   R0, A
  INC   DPTR
  MOVX  A, @DPTR
  MOV   R1, A
  ;RESTORE REGISTERS & RETURN
  POP   DPH
  POP   DPL
  CLR   C
  RET
ENDP


;WRITE TO 8155 TIMER
;ON ENTRY:
; R0    = TIMER LSB
; R1    = TIMER MSB
; DPTR  = 8155 BASE ADDRESS
;ON RETURN:
; R0    = VALUE ON ENTRY
; R1    = VALUE ON ENTRY
; DPTR  = VALUE ON ENTRY
; C     = 0 IF SUCCESS
;   A = 0x00
; C     = 1 IF FAIL
;   A = ERROR CODE
PIO8155TMRWR  PROC
  ;SAVE REGISTERS
  PUSH  DPL
  PUSH  DPH
  ;CALCULATE ADDRESS OF PORT'S REGISTER
  MOV   A, DPL
  ADD   A, #PIO_8155_REGISTER_TMR_LO
  MOV   DPL, A
  MOV   A, DPH
  ADDC  A, #0x00
  MOV   DPH, A
  ;WRITE TO PORT
  MOV   A, R0
  MOVX  @DPTR, A
  INC   DPTR
  MOV   A, R1
  MOVX  @DPTR, A
  ;RESTORE REGISTERS & RETURN
  POP   DPH
  POP   DPL
  CLR   C
  RET
ENDP

END
